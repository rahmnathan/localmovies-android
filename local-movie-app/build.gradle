apply plugin: 'com.google.devtools.ksp'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdk 36

    defaultConfig {
        applicationId "rahm.nathan.localmovies"
        minSdkVersion 26
        targetSdkVersion 36

        def env = System.getenv()
        def buildNumber = env['BUILD_NUMBER']
        versionName buildNumber ? "4." + buildNumber : "1"
        versionCode buildNumber ? 400 + buildNumber.toInteger() : 1

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
    defaultConfig {
        multiDexEnabled true
    }
    kotlinOptions {
        jvmTarget = '21'
    }
    buildFeatures {
        compose true
    }
    namespace 'rahmnathan.localmovies'
    lint {
        // Media3 unstable API opt-in is handled at file level
        disable 'UnsafeOptInUsageError'
    }
    applicationVariants.configureEach { variant ->
        variant.getRuntimeConfiguration().exclude group: 'com.google.guava', module: 'listenablefuture'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://artifactory.nathanrahm.com/artifactory/rahmnathan-libraries"
    }
}

apply plugin: 'com.google.gms.google-services'

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Firebase
    implementation 'com.google.firebase:firebase-messaging:25.0.1'

    // OAuth2
    implementation 'com.github.rahmnathan.oauth2:keycloak-resilience4j:2.12:jdk8'

    // AndroidX Core
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.mediarouter:mediarouter:1.8.1'
    implementation "androidx.recyclerview:recyclerview:1.4.0"
    implementation "androidx.annotation:annotation:1.9.1"
    implementation "androidx.constraintlayout:constraintlayout:2.2.1"

    // Compose BOM
    def composeBom = platform('androidx.compose:compose-bom:2026.02.00')
    implementation composeBom
    androidTestImplementation composeBom

    // Compose
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.activity:activity-compose:1.12.4'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.10.0'
    implementation 'androidx.navigation:navigation-compose:2.9.7'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // Hilt
    implementation 'com.google.dagger:hilt-android:2.58'
    ksp 'com.google.dagger:hilt-compiler:2.58'
    // Workaround for Kotlin 2.3.0 compatibility with Hilt (https://github.com/google/dagger/issues/5059)
    ksp 'org.jetbrains.kotlin:kotlin-metadata-jvm:2.3.0'
    implementation 'androidx.hilt:hilt-navigation-compose:1.3.0'
    compileOnly group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.10.2'

    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.10.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.10.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.10.0'

    // Coil for image loading
    implementation 'io.coil-kt.coil3:coil-compose:3.3.0'
    implementation 'io.coil-kt.coil3:coil-network-okhttp:3.3.0'

    // DataStore
    implementation 'androidx.datastore:datastore-preferences:1.2.0'

    // Ktor client
    implementation 'io.ktor:ktor-client-android:3.4.0'
    implementation 'io.ktor:ktor-client-content-negotiation:3.4.0'
    implementation 'io.ktor:ktor-serialization-gson:3.4.0'
    implementation 'io.ktor:ktor-client-auth:3.4.0'
    implementation 'io.ktor:ktor-client-logging:3.4.0'

    // Room with ktx for coroutines
    def room_version = "2.8.4"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    // Google Cast
    implementation 'com.google.android.gms:play-services-cast-framework:22.3.0'

    // Media3 ExoPlayer
    def media3_version = "1.9.2"
    implementation "androidx.media3:media3-exoplayer:$media3_version"
    implementation "androidx.media3:media3-ui:$media3_version"
    implementation "androidx.media3:media3-exoplayer-hls:$media3_version"
    implementation "androidx.media3:media3-common:$media3_version"

    // Other
    implementation 'com.google.guava:guava:33.5.0-jre'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

googleServices { disableVersionCheck = false }